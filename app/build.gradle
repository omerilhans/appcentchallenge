apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion versions.sdk.compile
    buildToolsVersion versions.android.buildtools

    def versionPropsFile = file('app_version.properties')
    if (!versionPropsFile.canRead()) {
        throw new GradleException("Could not read app_version.properties!")
    }

    Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(versionPropsFile))

    def versionMajor = versionProps['version_major'].toInteger()
    def versionMinor = versionProps['version_minor'].toInteger()
    def versionPatch = versionProps['version_patch'].toInteger()
    def versionBuild = versionProps['version_build'].toInteger()

    def taskNames = gradle.startParameter.taskNames
    String currentDir = gradle.startParameter.currentDir

    if (currentDir.endsWith(project.name) && ('bundle' in taskNames || 'assemble' in taskNames)) {
        println "+ Task names are $taskNames"
        versionBuild++
        versionProps['version_build'] = versionBuild.toString()
        versionProps.store(versionPropsFile.newWriter(), null)
        println "+ The build number is $versionBuild for ${project.name}"
    }

    defaultConfig {
        applicationId "com.omerilhanli.appcentchallenge"
        minSdkVersion versions.sdk.minSdkVersion
        targetSdkVersion versions.sdk.target
        versionCode versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        setProperty("archivesBaseName", "appcentchallenge-$versionName.$versionCode")
    }

    buildTypes {
        debug {
            buildConfigField 'String', 'BASE_URL', '"https://api.flickr.com/services/rest/"'
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            buildConfigField 'String', 'BASE_URL', '"https://api.flickr.com/services/rest/"'
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    sourceSets {
        main.java.srcDirs = ['src/main/kotlin']
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // Local module
    implementation project(':mdl-api')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$versions.kotlin.base"

    // Support
    implementation "androidx.core:core-ktx:$versions.androidx.ktxcore"
    implementation "androidx.appcompat:appcompat:$versions.androidx.appcompat"
    implementation "androidx.constraintlayout:constraintlayout:$versions.androidx.constraintlayout"
    implementation "com.google.android.material:material:$versions.androidx.material"
    implementation "androidx.recyclerview:recyclerview:$versions.androidx.recyclerview"
    implementation "androidx.cardview:cardview:$versions.androidx.cardview"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$versions.androidx.swipe_refresh_layout"
    implementation "androidx.browser:browser:$versions.androidx.browser"

    // LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$versions.androidx.lifecycle"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$versions.androidx.lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.androidx.lifecycle_viewmodel_ktx"

    // Retrofit2
    implementation "com.squareup.retrofit2:converter-gson:$versions.square.retrofit2"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$versions.square.retrofit2"
    implementation "com.squareup.okhttp3:okhttp:$versions.square.okhttp3"
    implementation "com.squareup.okhttp3:logging-interceptor:$versions.square.okhttp3"

    // Dagger
    implementation "com.google.dagger:dagger:$versions.google.dagger"
    kapt "com.google.dagger:dagger-compiler:$versions.google.dagger_core"
    kapt "com.google.dagger:dagger-android-processor:$versions.google.dagger_core"
    implementation "com.google.dagger:dagger-android-support:$versions.google.dagger_core"

    // Glide
    kapt "com.github.bumptech.glide:compiler:$versions.glide.kapt"
    implementation "com.github.bumptech.glide:glide:$versions.glide.base"
}
